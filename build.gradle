plugins {
    id "org.jetbrains.kotlin.jvm" version "1.7.10"
    id "java"
}

version = "${project_Version}"
group = "${project_Group}"
archivesBaseName = project_BaseName

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
tasks.withType(JavaCompile)*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io/" }
    maven { url "https://jcenter.bintray.com/" }
}

configurations {
    include
    implementation.extendsFrom include
}

dependencies {
    include 'commons-io:commons-io:2.11.0'
    include 'commons-cli:commons-cli:1.5.0'

    include 'com.fifesoft:rsyntaxtextarea:3.2.0'
    include 'club.minnced:java-discord-rpc:2.0.2'
    include 'org.bitbucket.mstrobel:procyon-core:0.6.0'
    include 'org.bitbucket.mstrobel:procyon-expressions:0.6.0'
    include 'org.bitbucket.mstrobel:procyon-reflection:0.6.0'
    include 'org.bitbucket.mstrobel:procyon-compilertools:0.6.0'
    include 'org.tinyjee.jgraphx:jgraphx:3.4.1.3'
    include 'org.jfree:jfreechart:1.5.3'
    include 'org.benf:cfr:0.152'
    include 'com.intellij:forms_rt:7.0.3'
    include 'org.apache.ant:ant:1.10.12'
    include 'com.google.guava:guava:31.1-jre'
    include 'com.google.code.gson:gson:2.9.1'
    include 'net.java.dev.jna:jna:5.12.1'
    include 'com.formdev:flatlaf:3.0'
    include files('libs/attach-1.7.jar')
    include files('libs/weblaf-complete-1.29-fixed.jar')

    include 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10'
}

task shadowJar(type: Jar) {
    classifier 'release'
    with jar
    from {
        configurations.include.collect {
            zipTree(it).matching {
                // exclude possible duplicates
                exclude 'META-INF/**', 'module-info.class'
                exclude 'darwin/**', 'libdiscord-rpc.dylib'
                exclude 'linux-x86-64/**', 'libdiscord-rpc.so'
                exclude 'win32-x86/**', 'discord-rpc.dll'
                exclude 'win32-x86-64/**', 'discord-rpc.dll'
                exclude 'org/objectweb/asm/**', 'AnnotationVisitor.class'
            }
        }
    }
    // set main class
    manifest {
        attributes(
                "Main-Class": "me.synnk.jbytecustom.JByteCustom"
        )
    }
}

jar.dependsOn(shadowJar)